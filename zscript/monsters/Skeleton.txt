class Voxel_Skeleton : Actor replaces DoomImp
{
	int clackTimer;
	Default
	{
	Health 100;
	Radius 7;
	Height 30;
	Mass 100;
	Speed 5;
	PainChance "PlayerSword", 255;
	DamageFactor "PlayerSword", 1;
	PainChance "PlayerArrow", 255;
	DamageFactor "PlayerArrow", 1;
	PainChance "Extreme", 255;
	DamageFactor "Extreme", 1;
	MaxStepHeight 9;
	Monster;
	+INTERPOLATEANGLES
	+NOBLOOD
	}
	States
	{
	Spawn:
		FRAM A 0;
		FRAM A 3 A_LookEx(0,0,256,256,120,"See");
		FRAM A 3 A_Jump(64,"Turn");
		FRAM A 3 A_Jump(64,"Wander");
		Loop;
	Wander:
		FRAM ABCBADED 3
		{
			//Have a sight distance of only 96 units
			A_LookEx(0,0,256,256,120,"See");
			A_Wander();
			
			//Let out a periodic bone clack sound effect
			if (clackTimer > 0)
			{
				clackTimer--;
			}
			else
			{
				A_PlaySound("sounds/skeleton/clack");
				clackTimer = random(30,50);
			}
			return null;
		}
		FRAM A 0 A_Jump(96,"Wander");
		Goto Spawn;
	Turn:
		FRAM A 3 A_LookEx(0,0,256,256,120,"See");
		FRAM A 3 A_SetAngle(random(-90.0,90.0),SPF_INTERPOLATE);
		Goto Spawn;
	See:
		FRAM ABCBADED 3 
		{
			A_Chase();
			//A_LookEx(0,128,0,128,120,"Wander");
			
			//Let out a periodic bone clack sound effect
			if (clackTimer > 0)
			{
				clackTimer--;
			}
			else
			{
				A_PlaySound("sounds/skeleton/clack");
				clackTimer = random(10,20);
			}
			return null;
		}
		Loop;
	Missile:
		FRAM FFF 4 A_FaceTarget;
		FRAM F 2
		{
			A_PlaySound("sounds/bow");
			A_SpawnProjectile("VoxelMobArrow",23);
			//A_SpawnItemEx("VoxelMobArrow",5,3,23,10,random(-0.5,0.5),random(-0.5,0.5));
		}
		FRAM GAA 4 A_FaceTarget;
		Goto See;	
	Pain.PlayerSword:
	Pain.PlayerArrow:
	Pain.Extreme:
		FRAM Z 8 A_PlaySound("sounds/skeleton/hurt");
		Goto See;
	Death.PlayerArrow:
	Death.Extreme:
	Death.PlayerSword:
		TNT1 A -1
		{
			A_PlaySound("sounds/skeleton/death");
			A_SpawnItemEx("VoxelSkull", 0, 0, 24, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelLBone", 0, -4, 23, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelLBone", 0, -2, 11, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelRBone", 0, 4, 23, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelRBone", 0, 2, 11, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelRib", 0, 0, 21, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelRibSingle", 0, 0, 22, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelRibSingle", 0, 0, 20, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
			A_SpawnItemEx("VoxelPelvis", 0, 0, 16, FRandom(-2,2), FRandom(-2,2), FRandom(0,1));
		}
		Stop;
	}
}

class VoxelMobArrow : Actor
{
	Default
	{
	Radius 2;
	Height 2;
	Speed 10;
	Damage (5);
	Projectile;
	Gravity 0.25;
	//DamageType "MobArrow";
	-NOGRAVITY
	+EXPLODEONWATER
	+FLATSPRITE
	+MTHRUSPECIES
	}
	States
	{
	Spawn:
		FRAM A 1; //NoDelay A_FaceTarget(0,0,180,0,0,8);
	Fly:
		FRAM A 1;
		Loop;
	Death:
		FRAM A 0 A_PlaySound("sounds/arrowhit",0);
		//FRAM A 1 A_SpawnItemEx("ArrowAmmo",0,0,0,0,0,0,0,0,224);
		//FRAM A 1 A_SpawnItemEx("VoxelArrowStuck", 0, 0, 2, 0, 0, 0, 0, SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION);
	XDeath:
		FRAM A 0 A_PlaySound("sounds/arrowhit",0);
		Stop;
	}
}